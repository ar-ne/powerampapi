apply plugin: 'com.android.application'

android {
    compileSdk COMPILE_SDK
    namespace "com.poweramp.v3.sampleskin"

    defaultConfig {
        minSdk MIN_SDK
        targetSdk TARGET_SDK
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    lint {
        disable 'ResAuto', 'MissingTranslation', 'OnClick', 'ExtraTranslation'
    }

    androidResources {
        def localProps = new Properties()
        localProps.load(rootProject.file("local.properties").newDataInputStream())
        def paResources = localProps['poweramp.apk'] ?: "Poweramp-build-978-resources.zip"
        println "Using $paResources PA resources"

        def params = []

        params += ["--emit-ids", "${projectDir}/stable-ids.txt", "--package-id", "0x80",  "-I", "${projectDir}/../${paResources}"]

        if(project.file("stable-ids.txt").exists()) {
            println "Got stable-ids OK"
            params += ["--stable-ids", "${projectDir}/stable-ids.txt"]
        } else {
            println "WARNING: no stable-ids.txt specified"
        }

        additionalParameters = params
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat-resources:1.6.1"
}
